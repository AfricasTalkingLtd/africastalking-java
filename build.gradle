buildscript {
	repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
	}
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

Set<String> publishedLibs = new HashSet<String>()
publishedLibs << 'sms'
publishedLibs << 'chat'
publishedLibs << 'mobileData'
publishedLibs << 'token'
publishedLibs << 'airtime'
publishedLibs << 'voice'
publishedLibs << 'insights'
publishedLibs << 'ussd'
publishedLibs << 'application'
publishedLibs << 'core'
configure(subprojects.findAll {publishedLibs.contains(it.name)}){
    group 'com.github.AfricasTalkingLtd'
    version '3.5.1'

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    jar {
        manifest {
            attributes 'Implementation-Title': 'Africa\'s Talking SDK',
                    'Implementation-Version': version
        }
    }

    javadoc {
        source = sourceSets.main.allJava
        // classpath = configurations.implementation
        options {
            links "http://docs.oracle.com/javase/8/docs/api/"
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    shadowJar {
        classifier = null // Avoid classifiers for now...
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    publishing {
        publications {
            SDK(MavenPublication) {
                artifact shadowJar
                groupId project.group
                artifactId project.name
                version project.version
            }
        }
    }
}
