apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'
mainClassName = "com.africastalking.SimpleServer"

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.2.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.3.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/gen"
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
        // If you have additional sourceSets and/or codegen plugins, add all of them
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
    }
}

sourceSets {
    main {
        proto {
            srcDir '../common/src/main/proto'
        }
    }
}

dependencies {

    compile project(":core")

    compile 'org.hashids:hashids:1.0.2'

    // grpc
    compile 'io.grpc:grpc-stub:1.3.0'

    compile 'io.grpc:grpc-netty:1.3.0' // Server+Client
    // compile 'io.grpc:grpc-okhttp:1.3.0' Client on Android

    compile 'io.grpc:grpc-protobuf:1.3.0' // Server+Client
    // compile 'io.grpc:grpc-protobuf-lite:1.3.0' Client on Android

    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def username = properties.getProperty('username')
def apiKey = properties.getProperty('apiKey')
def tokenCount = properties.getProperty('tokenCount')

task runServer(type: JavaExec, dependsOn: classes){
    def params = new String[3]
    params[0] = username
    params[1] = apiKey
    params[2] = tokenCount
    args(params)
    description = "Simple gRPC Server"
    main = "com.africastalking.SimpleServer"
    classpath = sourceSets.main.runtimeClasspath
}
